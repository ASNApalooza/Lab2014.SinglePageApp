<div class="page-header">
    <h2>Part 2: Data</h2>
</div>
<p>
    The live lab at ASNApalooza 2014 will include a local DataGate database. The
    project also includes a local SQL database that can be used if you do not have
    access to the DataGate database. This section will cover how to connect with
    each.
</p>
<p>
    In the last section, we defined a contact entity and a repository interface.
    Now we need to implement the interface for DataGate and SQL. Whether you are
    more familiar with DataGate or SQL, these examples should illustrate that
    other implementations are possible as well.
</p>
<p>
    Create a <code>Data</code> folder within the <code>App_Code</code> folder using
    the process outlined in
    <a href="/Lab2014.SinglePageApp/#tutorial/part-1-domain" data-asna-nav="route:tutorial:domain">part 1</a>.
</p>
<p>
    Create a new class in the <code>Data</code> folder called
    <code>DataGateContactRepository</code>. If you are familiar with DataGate, the
    following code should be self-explanatory:
</p>
<div class="panel-group" id="datagate-repo-source">
    <div class="panel panel-default">
        <div class="panel-heading">
            <h4 class="panel-title">
                <a data-toggle="collapse" data-parent="#datagate-repo-source" href="#datagate-repo-source-body">
                    <code>App_Code\Data\DataGateContactRepository.vr</code>
                </a>
            </h4>
        </div>
        <div id="datagate-repo-source-body" class="panel-collapse collapse">
            <div class="panel-body">
<pre class="prettyprint linenums">
Using System
Using System.Data
Using System.Text
Using System.Collections.Generic
Using ASNA.DataGate.Client

BegClass DataGateContactRepository Access(*Public) Implements(IContactRepository)

    DclConst DBName Value("*Public/DG NET Local")

    DclDB pgmDB DBName("*Public/DG NET Local")

    DclDiskFile CustomerFile +
        Type(*Update) +
        Org(*Indexed) +
        Prefix(CUSTOMER_) +
        File("RPDB/CUSTOMER") +
        DB(pgmDB) +
        ImpOpen(*No) +
        AddRec(*Yes)

    DclDiskFile CustomerFileByEmail +
        Type(*Update) +
        Org(*Indexed) +
        Prefix(CUSTOMER_) +
        File("RPDB/CUSTOMERL3") +
        DB(pgmDB) +
        ImpOpen(*No) +
        AddRec(*Yes) +
        RnmFmt(RCUST, RCUSTY)

    DclDiskFile CustomerInput +
        Type(*Input) +
        Org(*Indexed) +
        Prefix(CUSTOMER_) +
        File("RPDB/CUSTOMER") +
        DB(pgmDB) +
        ImpOpen(*No) +
        RnmFmt(RCUST, RCUSTX)

    DclMemoryFile CustomerMF +
        DBDesc("*PUBLIC/DG NET Local") +
        Prefix(CUSTOMER_) +
        FileDesc("RPDB/CUSTOMER") +
        ImpOpen(*YES) +
        RnmFmt(RCUSTMF)

    BegFunc FindContacts Type(List(*Of Contact)) Access(*Public) Implements(IContactRepository.FindContacts)
        DclFld contacts Type(List(*Of Contact)) New()
        DclFld dt Type(DataTable)
        DclFld contactEntity Type(Contact)

        Connect pgmDB
        Open CustomerInput

        Read CustomerInput
        DoWhile NOT CustomerInput.IsEof
            Write CustomerMF
            Read CustomerInput
        EndDo

        dt = CustomerMF.DataSet.Tables[0]

        ForEach dr Type(DataRow) Collection(dt.Rows)
            contactEntity = GetContactInstance(dr)
            contacts.Add(contactEntity)
        EndFor

        Close CustomerInput
        Disconnect pgmDB

        LeaveSr contacts
    EndFunc

    BegFunc FindContact Type(Contact) Access(*Public) Implements(IContactRepository.FindContact)
        DclSrParm id Type(*Integer4)

        DclFld contactEntity  Type(Contact)
        DclFld dt Type(DataTable)

        Connect pgmDB
        Open CustomerInput

        Chain CustomerInput Key(id)
        If (CustomerInput.IsFound)
            Write CustomerMF
            dt = CustomerMF.DataSet.Tables[0]
            contactEntity = GetContactInstance(dt.Rows[0])
        EndIf

        Close CustomerInput
        Disconnect pgmDB

        LeaveSr contactEntity
    EndFunc

    BegFunc FindContactByEmail Type(Contact) Access(*Public) Implements(IContactRepository.FindContactByEmail)
        DclSrParm email Type(*String)

        DclFld contactEntity Type(Contact)
        DclFld dt Type(DataTable)

        Connect pgmDB
        Open CustomerFileByEmail

        Chain CustomerFileByEmail Key(email)
        If (CustomerFileByEmail.IsFound)
            Write CustomerMF
            dt = CustomerMF.DataSet.Tables[0]
            contactEntity = GetContactInstance(dt.Rows[0])
        EndIf

        Close CustomerFileByEmail
        Disconnect pgmDB

        LeaveSr contactEntity
    EndFunc

    BegSr AddContact Access(*Public) Implements(IContactRepository.AddContact)
        DclSrParm contactEntity Type(Contact)

        pgmDB.DBName = DBNAME
        Connect pgmDB
        Open CustomerFile

        contactEntity.id = GetNextId()
        AssignFields(contactEntity)
        Write CustomerFile

        Close CustomerFile
        Disconnect pgmDB
    EndSr

    BegSr UpdateContact Access(*Public) Implements(IContactRepository.UpdateContact)
        DclSrParm contactEntity Type(Contact)

        pgmDB.DBName = DBNAME
        Connect pgmDB
        Open CustomerFile

        Chain CustomerFile Key(contactEntity.id)
        If CustomerFile.IsFound
            AssignFields(contactEntity)
            Update CustomerFile
        EndIf

        Close CustomerFile
        Disconnect pgmDB
    EndSr

    BegSr RemoveContact Access(*Public) Implements(IContactRepository.RemoveContact)
        DclSrParm contactEntity Type(Contact)

        pgmDB.DBName = DBNAME
        Connect pgmDB
        Open CustomerFile

        Chain CustomerFile Key(contactEntity.id)
        If CustomerFile.IsFound
            Delete CustomerFile
        EndIf

        Close CustomerFile
        Disconnect pgmDB
    EndSr

    BegFunc GetNextId Type(*Integer8)
        DclFld BOF Type(*Boolean)

        SetLL CustomerFile Key(*End)
        ReadP CustomerFile Eof(BOF)
        If (BOF)
            LeaveSr Value(1)
        Else
            LeaveSr Value(Customer_CMCustNo + 1)
        EndIf
    EndFunc

    BegFunc GetContactInstance Type(Contact)
        DclSrParm dr Type(DataRow)

        DclFld contactEntity Type(Contact) New()

        contactEntity.ID = dr['CMCUSTNO'] *As System.Decimal
        contactEntity.Company = (dr['CMNAME'] *As System.String).Trim()
        contactEntity.FirstName = (dr['CMFNAME'] *As System.String).Trim()
        contactEntity.LastName = (dr['CMLNAME'] *As System.String).Trim()
        contactEntity.Email = (dr['CMCONEMAIL'] *As System.String).Trim()
        contactEntity.Phone = (dr['CMPHONE'] *As System.Decimal).ToString().Trim()
        contactEntity.Address1 = (dr['CMADDR1'] *As System.String).Trim()
        If (String.IsNullOrEmpty((dr['CMADDR2'] *As System.String).Trim()))
            contactEntity.Address2 = *Nothing
        Else
            contactEntity.Address2 = (dr['CMADDR2'] *As System.String).Trim()
        EndIf
        contactEntity.City = (dr['CMCITY'] *As System.String).Trim()
        contactEntity.State = (dr['CMSTATE'] *As System.String).Trim()
        contactEntity.PostalCode = (dr['CMPOSTCODE'] *As System.String).Trim()

        LeaveSr contactEntity
    EndFunc

    BegSr AssignFields
        DclSrParm contactEntity Type(Contact)

        Customer_CMCUSTNO = contactEntity.ID
        Customer_CMNAME = contactEntity.Company.Trim()
        Customer_CMFNAME = contactEntity.FirstName.Trim()
        Customer_CMLNAME = contactEntity.LastName.Trim()
        Customer_CMCONEMAIL = contactEntity.Email.Trim()
        Customer_CMPHONE = contactEntity.Phone.Trim()
        Customer_CMADDR1 = contactEntity.Address1.Trim()
        If (contactEntity.Address2 = *Nothing)
            Customer_CMADDR2 = String.Empty
        Else
            Customer_CMADDR2 = contactEntity.Address2.Trim()
        EndIf
        Customer_CMCITY = contactEntity.City.Trim()
        Customer_CMSTATE = contactEntity.State.Trim()
        Customer_CMPOSTCODE = contactEntity.PostalCode.Trim()
    EndSr

EndClass
</pre>
            </div>
        </div>
    </div>
</div>
<p>
    The implements statement in the class declaration,
    <code>Implements(IContactRepository)</code>, is very important. Notice all
    of the interface methods include a similar statement:
</p>
<pre class="prettyprint">
    BegFunc FindContacts Type(List(*Of Contact)) Access(*Public) <strong>Implements(IContactRepository.FindContacts)</strong>
</pre>
<p>
    These statements tell the compiler that this class fulfills the &ldquo;contract&rdquo;
    defined in our repository interface.
</p>
<p>
    Create another class in the <code>Data</code> folder called <code>SqlContactRepository</code>.
    This class includes a different implementation of the same methods:
</p>
<div class="panel-group" id="sql-repo-source">
    <div class="panel panel-default">
        <div class="panel-heading">
            <h4 class="panel-title">
                <a data-toggle="collapse" data-parent="#sql-repo-source" href="#sql-repo-source-body">
                    <code>App_Code\Data\SqlContactRepository.vr</code>
                </a>
            </h4>
        </div>
        <div id="sql-repo-source-body" class="panel-collapse collapse">
            <div class="panel-body">
<pre class="prettyprint linenums">
Using System
Using System.Configuration
Using System.Collections.Generic
Using System.Data
Using System.Data.SqlClient
Using System.Text

BegClass SqlContactRepository Access(*Public) Implements(IContactRepository)

    DclFld connection Type(SqlConnection)

    BegConstructor Access(*Public)

        DclFld connectionString Type(*String)

        connectionString = ConfigurationManager.ConnectionStrings["Application"].ConnectionString
        connection = *New SqlConnection(connectionString)
    EndConstructor

    // Retrieves all Contacts
    BegFunc FindContacts Type(List(*Of Contact)) Access(*Public) Implements(IContactRepository.FindContacts)

        DclFld contacts Type(List(*Of Contact))
        DclFld contactEntity Type(Contact)
        DclFld command Type(SqlCommand)
        DclFld data Type(SqlDataReader)

        connection.Open()

        command = *New SqlCommand("SELECT * FROM Contacts;", connection)
        data = command.ExecuteReader()

        contacts = *New List(*Of Contact)()

        If (data.HasRows)
            DoWhile (data.Read())
                contactEntity = GetContactInstance(data)
                contacts.Add(contactEntity)
            EndDo
        EndIf

        data.Close()
        connection.Close()

        LeaveSr contacts
    EndFunc

    // Retrieves Contact by Id
    BegFunc FindContact Type(Contact) Access(*Public) Implements(IContactRepository.FindContact)
        DclSrParm id Type(*Integer4)

        DclFld contactEntity Type(Contact)
        DclFld command Type(SqlCommand)
        DclFld idParm Type(SqlParameter)
        DclFld data Type(SqlDataReader)

        connection.Open()

        command = *New SqlCommand("SELECT * FROM Contacts WHERE id = @id;", connection)
        idParm = *New SqlParameter("@id", SqlDbType.Int)
        idParm.Value = id
        command.Parameters.Add(idParm)
        data = command.ExecuteReader()

        If (data.HasRows)
            DoWhile (data.Read())
                contactEntity = GetContactInstance(data)
                data.Close()
                connection.Close()

                LeaveSr contactEntity
            EndDo
        EndIf

        data.Close()
        connection.Close()

        LeaveSr *Nothing
    EndFunc

    // Retrieves Contact by Email
    BegFunc FindContactByEmail Type(Contact) Access(*Public) Implements(IContactRepository.FindContactByEmail)
        DclSrParm email Type(*String)

        DclFld contactEntity Type(Contact)
        DclFld command Type(SqlCommand)
        DclFld emailParm Type(SqlParameter)
        DclFld data Type(SqlDataReader)

        connection.Open()

        command = *New SqlCommand("SELECT * FROM Contacts WHERE email = @email;", connection)
        emailParm = *New SqlParameter("@email", SqlDbType.NVarChar)
        emailParm.Value = email
        command.Parameters.Add(emailParm)
        data = command.ExecuteReader()

        If (data.HasRows)
            DoWhile (data.Read())
                contactEntity = GetContactInstance(data)
                data.Close()
                connection.Close()

                LeaveSr contactEntity
            EndDo
        EndIf

        data.Close()
        connection.Close()

        LeaveSr *Nothing
    EndFunc

    // Persists a new Contact
    BegSr AddContact Access(*Public) Implements(IContactRepository.AddContact)
        DclSrParm contactEntity Type(Contact)

        DclFld command Type(SqlCommand)
        DclFld statement Type(StringBuilder)

        connection.Open()

        statement = *New StringBuilder()
        statement.Append("INSERT INTO Contacts ")
        statement.Append("(company, firstName, lastName, email, phone, address1, address2, city, state, postalCode) ")
        statement.Append("VALUES (@company, @firstName, @lastName, @email, @phone, @address1, @address2, @city, @state, @postalCode);")

        command = *New SqlCommand(statement.ToString(), connection)

        AssignFields(contactEntity, command.Parameters)

        command.ExecuteNonQuery()

        connection.Close()
    EndSr

    // Updates a persisted Contact
    BegSr UpdateContact Access(*Public) Implements(IContactRepository.UpdateContact)
        DclSrParm contactEntity Type(Contact)

        DclFld command Type(SqlCommand)
        DclFld statement Type(StringBuilder)
        DclFld idParm Type(SqlParameter)

        connection.Open()

        statement = *New StringBuilder()
        statement.Append("UPDATE Contacts ")
        statement.Append("SET Company = @company, firstName = @firstName, lastName = @lastName, email = @email, phone = @phone, address1 = @address1, address2 = @address2, city = @city, state = @state, postalCode = @postalCode ")
        statement.Append("WHERE id = @id;")

        command = *New SqlCommand(statement.ToString(), connection)

        AssignFields(contactEntity, command.Parameters)

        idParm = *New SqlParameter("@id", SqlDbType.Int)
        idParm.Value = contactEntity.id
        command.Parameters.Add(idParm)

        command.ExecuteNonQuery()

        connection.Close()
    EndSr

    // Removes a persisted Contact
    BegSr RemoveContact Access(*Public) Implements(IContactRepository.RemoveContact)
        DclSrParm contactEntity Type(Contact)

        DclFld command Type(SqlCommand)
        DclFld statement Type(StringBuilder)
        DclFld idParm Type(SqlParameter)

        connection.Open()

        statement = *New StringBuilder()
        statement.Append("DELETE FROM Contacts ")
        statement.Append("WHERE id = @id;")

        command = *new SqlCommand(statement.ToString(), connection)

        idParm = *New SqlParameter("@id", SqlDbType.Int)
        idParm.Value = contactEntity.id
        command.Parameters.Add(idParm)

        command.ExecuteNonQuery()

        connection.Close()
    EndSr

    BegSr AssignFields Access(*Protected)
        DclSrParm contactEntity Type(Contact)
        DclSrParm params Type(SqlParameterCollection)

        DclFld companyParm Type(SqlParameter)
        DclFld firstNameParm Type(SqlParameter)
        DclFld lastNameParm Type(SqlParameter)
        DclFld emailParm Type(SqlParameter)
        DclFld phoneParm Type(SqlParameter)
        DclFld address1Parm Type(SqlParameter)
        DclFld address2Parm Type(SqlParameter)
        DclFld cityParm Type(SqlParameter)
        DclFLd stateParm Type(SqlParameter)
        DclFld postalCodeParm Type(SqlParameter)

        companyParm = *New SqlParameter("@company", SqlDbType.NVarChar)
        companyParm.Value = contactEntity.company.Trim()
        params.Add(companyParm)

        firstNameParm = *New SqlParameter("@firstName", SqlDbType.NVarChar)
        firstNameParm.Value = contactEntity.firstName.Trim()
        params.Add(firstNameParm)

        lastNameParm = *New SqlParameter("@lastName", SqlDbType.NVarChar)
        lastNameParm.Value = contactEntity.lastName.Trim()
        params.Add(lastNameParm)

        emailParm = *New SqlParameter("@email", SqlDbType.NVarChar)
        emailParm.Value = contactEntity.email.Trim()
        params.Add(emailParm)

        phoneParm = *New SqlParameter("@phone", SqlDbType.NVarChar)
        phoneParm.Value = contactEntity.phone.Trim()
        params.Add(phoneParm)

        address1Parm = *New SqlParameter("@address1", SqlDbType.NVarChar)
        address1Parm.Value = contactEntity.address1.Trim()
        params.Add(address1Parm)

        address2Parm = *New SqlParameter("@address2", SqlDbType.NVarChar)
        If (String.IsNullOrEmpty(contactEntity.address2))
            address2Parm.Value = DBNull.Value
        Else
            address2Parm.Value = contactEntity.address2.Trim()
        Endif
        params.Add(address2Parm)

        cityParm = *New SqlParameter("@city", SqlDbType.NVarChar)
        cityParm.Value = contactEntity.city.Trim()
        params.Add(cityParm)

        stateParm = *New SqlParameter("@state", SqlDbType.NVarChar)
        stateParm.Value = contactEntity.state.Trim()
        params.Add(stateParm)

        postalCodeParm = *New SqlParameter("@postalCode", SqlDbType.NVarChar)
        postalCodeParm.Value = contactEntity.postalCode.Trim()
        params.Add(postalCodeParm)
    EndSr

    BegFunc GetContactInstance Type(Contact) Access(*Protected)
        DclSrParm data Type(SqlDataReader)

        DclFld contactEntity Type(Contact)

        contactEntity = *New Contact()

        contactEntity.id = Convert.ToInt32(Data["id"])
        contactEntity.company = data["company"].ToString().Trim()
        contactEntity.firstName = data["firstName"].ToString().Trim()
        contactEntity.lastName = data["lastName"].ToString().Trim()
        contactEntity.email = data["email"].ToString().Trim()
        contactEntity.phone = data["phone"].ToString().Trim()
        contactEntity.address1 = data["address1"].ToString().Trim()
        If (String.IsNullOrEmpty(data["address2"].ToString()))
            contactEntity.address2 = *Nothing
        Else
            contactEntity.address2 = data["address2"].ToString().Trim()
        EndIf
        contactEntity.city = data["city"].ToString().Trim()
        contactEntity.state = data["state"].ToString().Trim()
        contactEntity.postalCode = data["postalCode"].ToString().Trim()

        LeaveSr contactEntity
    EndFunc

EndClass
</pre>
            </div>
        </div>
    </div>
</div>
<p>
    To get the full benefits of the repository interface, we need a way to use
    it without knowing specifically which type it is. Many modern applications
    use <a href="http://en.wikipedia.org/wiki/Dependency_injection">dependency injection</a>
    to pass objects into a class constructor. In our case, it may be easier to
    create a simple <a href="http://martinfowler.com/eaaCatalog/registry.html">Registry</a>
    class.
</p>
<p>
    Create a new class called <code>Registry</code> in the <code>App_Code</code>
    folder. This simple class needs one method: <code>GetContactRepository</code>
    which should return <em>some implementation</em> of <code>IContactRepository</code>.
    Our controller will not care which object it receives so it only depends on the
    interface.
</p>
<blockquote>
    Depend upon Abstractions. Do not depend upon concretions.
    <footer>
        Robert C. Martin
    </footer>
</blockquote>
<p>
    The <code>Registry</code> class should let us easily swap implementations:
</p>
<div class="panel-group" id="registry-source">
    <div class="panel panel-default">
        <div class="panel-heading">
            <h4 class="panel-title">
                <a data-toggle="collapse" data-parent="#registry-source" href="#registry-source-body">
                    <code>App_Code\Registry.vr</code>
                </a>
            </h4>
        </div>
        <div id="registry-source-body" class="panel-collapse collapse">
            <div class="panel-body">
<pre class="prettyprint linenums">
Using System
Using System.Data
Using System.Data.SqlClient
Using System.Configuration
Using System.Web

BegClass Registry Access(*Public)

    DclFld contactRepo Type(IContactRepository) Access(*Protected)

    // Retrieves the ContactRepository service
    BegFunc GetContactRepository Type(IContactRepository) Access(*Public)
        If (contactRepo = *Nothing)
            // Uses DataGate
            contactRepo = *New DataGateContactRepository()
            // Uses SQL Server
            // contactRepo = *New SqlContactRepository()
        EndIf

        LeaveSr contactRepo
    EndFunc

EndClass
</pre>
            </div>
        </div>
    </div>
</div>
<p>
    If you want to use the SQL Server database, just comment-out line 15 and
    uncomment line 17.
</p>
<p>
    In the next part, we will create the &ldquo;page&rdquo; in our Single-page
    application. Click the <strong>Next</strong> button below to continue.
</p>
<hr>
<ul class="pager">
    <li class="previous"><a href="/Lab2014.SinglePageApp/#tutorial/part-1-domain" data-asna-nav="route:tutorial:domain">&larr; Previous</a></li>
    <li class="next"><a href="/Lab2014.SinglePageApp/#tutorial/part-3-page" data-asna-nav="route:tutorial:page">Next &rarr;</a></li>
</ul>
<hr>